#!/usr/bin/env bb

;; Standalone script for running makejack.

;; By default, uses makejack's built in tasks, or tasks from build.clj if it
;; exists.

;; To use tasks from a different namespace,
;; pass `--tasks <path-to-ns-defining-file>`.

;; workaround to enable loading of polylith clj api
(require 'clojure.pprint)
(intern 'clojure.pprint 'code-dispatch clojure.pprint/simple-dispatch)

(let [orig-ns (ns-name *ns*)]
  (ns polylith.clj.core.common.class-loader)
  (defn create-class-loader [_ _])
  (defn eval-in [_ _])
  (ns borkdude.dynaload)
  (defn dynaload [_ _])
  (ns malli.core)
  (defn explain [_ _])
  (ns malli.util)
  (ns malli.error)
  (defn humanize [_])
  (ns polylith.clj.core.lib.maven-dep)
  (defn latest [_ _ _])

  (ns puget.printer)
  (defn cprint [_ _])
  (in-ns orig-ns))


(require '[babashka.fs :as fs])
(require '[babashka.deps :as deps])
(require '[babashka.pods :as pods])

(deps/add-deps '{:deps
                 {org.clojure/tools.namespace
                  {:git/url "https://github.com/babashka/tools.namespace"
                   :git/tag "tools.namespace-1.1.0"
                   :git/sha "56088a1"}}})


(deps/add-deps '{:deps
                 {io.github.clojure/tools.build
                  {:git/url    "https://github.com/babashka/tools.bbuild"
                   :git/sha    "a3c0723b6250756eaeed532ade5d6474df24e80a"
                   :exclusions [org.clojure/tools.namespace]}}})

(pods/load-pod 'org.babashka/tools-deps-native "0.1.0")

(require 'clojure.edn)
(require 'clojure.tools.build.api)
(require 'clojure.tools.deps)

(deps/add-deps
 '{:deps {io.github.hugoduncan/makejack
          {:git/sha    "6966696979c9b9c0c97e54f82a5d4096e4105240"
           :deps/root  "projects/makejack-jar"
           :exclusions [io.github.clojure/tools.build]}}})


(defn parse-namespace [path]
  (-> (clojure.edn/read-string (slurp path))
      second))

(defn read-or-identity [x]
  (try (clojure.edn/read-string x)
       (catch Exception _
         x)))

(defn parse-opts []
  (let [args             *command-line-args*
        [args task-file] (cond
                           (= "--tasks" (first args))
                           [(drop 2 args) (second args)]

                           (fs/exists? "build.clj")
                           [args "build.clj"]

                           :else
                           [args nil])
        ns-str (if task-file
                 (or (some->
                      (parse-namespace task-file)
                      str)
                     (assert false "Task file must contain a ns form"))
                 "makejack.tasks")
        f      (symbol (or (first args) "help"))
        f      (if (namespace f) f (symbol ns-str (name f)))
        ns-str (namespace f)]
    {:task-file task-file
     :ns-str    ns-str
     :f         f
     :args      (into {} (->> args
                              rest
                              (mapv read-or-identity)
                              (partition 2)
                              (mapv vec)))}))


(let [{:keys [task-file ns-str f args]} (parse-opts)]
  (if task-file
    (load-file task-file)
    (require 'makejack.tasks))
  (require (symbol ns-str))
  ((resolve f) args))

nil
