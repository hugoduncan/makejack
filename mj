#!/usr/bin/env bb

;; Standalone script for running makejack.

;; By default, uses makejack's built in tasks.

;; To use tasks from a different namespace,
;; pass `--tasks <path-to-ns-defining-file>`.


(require '[babashka.fs :as fs])
(require '[babashka.deps :as deps])
(require '[babashka.pods :as pods])

(pods/load-pod "tools-deps-native")

(deps/add-deps
 '{:deps
   {borkdude/spartan.spec
    {:git/url "https://github.com/borkdude/spartan.spec"
     :sha     "12947185b4f8b8ff8ee3bc0f19c98dbde54d4c90"}}})

(require 'spartan.spec)

(deps/add-deps '{:deps
                 {org.clojure/tools.namespace
                  {:git/url "https://github.com/babashka/tools.namespace"
                   :git/sha "3625153ee66dfcec2ba600851b5b2cbdab8fae6c"}}})


(deps/add-deps '{:deps
                 {io.github.clojure/tools.build
                  {:git/url    "https://github.com/babashka/tools.bbuild"
                   :git/sha    "af73f5a1ffe209291aa80215495a7959e1032d39"
                   :exclusions [org.clojure/tools.namespace]}}})

(require 'clojure.tools.build.api)
(require 'clojure.tools.deps.alpha)

(deps/add-deps
 '{:deps {io.github.hugoduncan/makejack
          #_ {:local/root "projects/makejack-jar"}
          {:git/sha    "29bd56233997bd1b9a2140e562f6364251b549a6"
           :deps/root  "projects/makejack-jar"
           :exclusions [io.github.clojure/tools.build]}}})

(defn parse-namespace [path]
  (-> (clojure.edn/read-string (slurp path))
      second))

(defn read-or-identity [x]
  (try (edn/read-string x)
       (catch Exception _
         x)))

(defn parse-opts []
  (let [args             *command-line-args*
        [args task-file] (cond
                           (= "--tasks" (first args))
                           [(drop 2 args) (second args)]

                           (fs/exists? "build.clj")
                           [args "build.clj"]

                           :else
                           [args nil])
        ns-str (if task-file
                 (or (some->
                      (parse-namespace task-file)
                      str)
                     (assert false "Task file must contain a ns form"))
                 "makejack.tasks")
        f      (symbol (or (first args) "help"))
        f      (if (namespace f) f (symbol ns-str (name f)))
        ns-str (namespace f)]
    {:task-file task-file
     :ns-str    ns-str
     :f         f
     :args      (into {} (->> args
                              rest
                              (mapv read-or-identity)
                              (partition 2)
                              (mapv vec)))}))


(let [{:keys [task-file ns-str f args]} (parse-opts)]
  (if task-file
    (load-file task-file)
    (require 'makejack.tasks))
  (require (symbol ns-str))
  ((resolve f) args))

nil
